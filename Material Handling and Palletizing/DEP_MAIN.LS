/PROG  DEP_MAIN
/ATTR
OWNER		= ASCBIN;
COMMENT		= "";
PROG_SIZE	= 4803;
CREATE		= DATE 25-01-16  TIME 16:06:00;
MODIFIED	= DATE 25-02-06  TIME 19:50:52;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 197;
MEMORY_SIZE	= 5367;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/MN
   1:  ! DATA REFERENCES ;
   2:  ! P[1] READY POSITION ;
   3:  ! TIMEOUT 2.5 SECONDS ;
   4:  ! -----INPUTS----- ;
   5:  ! DI[121] - BEGIN CYCLE ;
   6:  ! DI[122] - CONTINUE WITH CYCLE ;
   7:  ! DI[123] - RESTART RESOLVED ;
   8:  ! DI[124] - CONTINUE RESTORE DATA ;
   9:  ! DI[125] - REQUEST NEW PALLET ;
  10:  ! DI[126] - VACUUM RESET ;
  11:  ! -----OUTPUTS----- ;
  12:  ! DO[121] - READY TO BEGIN CYCLE ;
  13:  ! DO[122] - IN CYCLE ;
  14:  ! DO[123] - DEPALLETIZING ;
  15:  ! DO[124] - PALLETIZING ;
  16:  ! DO[125] - ABORTED ;
  17:  ! DO[126] - Not Used (Reserved) ;
  18:  ! DO[127] - VACUUM ON AT RESTART ;
  19:  ! -----PALLET REGISTERS----- ;
  20:  ! PL[1] - DEPALLETIZE ;
  21:  ! PL[2] - PALLETIZING ;
  22:  ! -----DATA REGISTERS----- ;
  23:  ! R[1] - DEPALLET COUNT REGISTER ;
  24:  ! R[2] - PALLET COUNT REGISTER ;
  25:  ! R[3] - ERROR CODE REGISTER ;
  26:  ! R[6] - RETRY COUNT FOR ERROR HA ;
  27:  ! -----STACK GROUP OUTPUT----- ;
  28:  ! GO[11] - PARTS ON STACK, 9 to 0 ;
  29:  ! -----PALLET GROUP OUTPUT----- ;
  30:  ! GO[12] - PARTS ON PALLET, 0 to ;
  31:  ! -----ERROR GROUP OUTPUT----- ;
  32:  ! GO[13] - ERROR CODE ;
  33:  ! VALUE MEANING ;
  34:  ! 0 - No Error ;
  35:  ! 1 - (Reserved) ;
  36:  ! 2 - Part Pick-up Vacuum Failure ;
  37:  ! 3 - Vacuum Motion Error ;
  38:  ! 4 - (Reserved) ;
  39:  ! 5 - Part Drop-off Vacuum Failur ;
  40:  ! 6 - (Reserved) ;
  41:  ! 7 - Vacuum ON At Restart ;
  42:  ! --STACK RESTART GROUP INPUT-- ;
  43:  ! GI[11] - NUMBER OF PARTS AT STA ;
  44:  ! --PALLET RESTART GROUP INPUT-- ;
  45:  ! GI[12] - NUMBER OF PARTS AT STA ;
  46:  ! -----OnRobot FLAGS----- ;
  47:  ! F [14] - Channel A VACUUM ;
  48:  ! F [15] - Channel B VACUUM ;
  49:  ! ---OnRobot GROUP INPUTS--- ;
  50:  ! GI [47] - Channel A VACUUM FEED ;
  51:  ! GI [48] - Channel B VACUUM FEED ;
  52:  ! ---OnRobot GROUP OUTPUTS--- ;
  53:  ! GO [47] - Channel A VACUUM COMM ;
  54:  ! GO [48] - Channel B VACUUM COMM ;
  55:  ! LBL[1] Ready To Begin Cycle ;
  56:  ! ;
  57:  ! ;
  58:  ! IF STATEMENTS FOR RESTARTS PRIO ;
  59:  ! ;
  60:  ! ;
  61:  LBL[111] ;
  62:  IF (GO[47]>5 OR GO[48]>5) THEN ;
  63:  DO[127]=ON ;
  64:  GO[13]=7 ;
  65:  UALM[6] ;
  66:  WAIT DI[126]=ON    ;
  67:  UALM[1] ;
  68:  DO[127]=OFF ;
  69:  GO[13]=0 ;
  70:  JMP LBL[111] ;
  71:  ENDIF ;
  72:  ! ;
  73:  ! ;
  74:  IF (R[3:Dropped parts]>0) THEN ;
  75:  GO[13]=R[3:Dropped parts] ;
  76:  ENDIF ;
  77:  ! ;
  78:  ! ;
  79:  IF (GO[13]=7) THEN ;
  80:  CALL RELEASE(0,0) ;
  81:  CALL CHECK_VACUUM(0,0) ;
  82:  WAIT (F[14]=OFF AND F[15]=OFF)    ;
  83:  R[3:Dropped parts]=0    ;
  84:  GO[13]=R[3:Dropped parts] ;
  85:  DO[125]=OFF ;
  86:  ENDIF ;
  87:  ! ;
  88:  ! ;
  89:  IF (GO[13]=2) THEN ;
  90:  R[3:Dropped parts]=0    ;
  91:  GO[13]=R[3:Dropped parts] ;
  92:  DO[125]=OFF ;
  93:  LBL[777] ;
  94:  IF DI[125]=ON,JMP LBL[1] ;
  95:  IF DI[122]=ON,JMP LBL[2] ;
  96:  JMP LBL[777] ;
  97:  ENDIF ;
  98:  ! ;
  99:  ! ;
 100:  IF (GO[13]=3) THEN ;
 101:  R[3:Dropped parts]=0    ;
 102:  GO[13]=R[3:Dropped parts] ;
 103:  DO[125]=OFF ;
 104:  WAIT DI[123]=ON    ;
 105:  R[2:PALLET COUNT]=GI[12]    ;
 106:  IF R[2:PALLET COUNT]=9,JMP LBL[1] ;
 107:  CALL DEP_LOOKTBL    ;
 108:  LBL[778] ;
 109:  IF DI[125]=ON,JMP LBL[1] ;
 110:  IF DI[122]=ON,JMP LBL[2] ;
 111:  JMP LBL[778] ;
 112:  ENDIF ;
 113:  ! ;
 114:  ! ;
 115:  IF (GO[13]=5) THEN ;
 116:  R[3:Dropped parts]=0    ;
 117:  GO[13]=R[3:Dropped parts] ;
 118:  DO[125]=OFF ;
 119:  WAIT DI[123]=ON    ;
 120:  R[2:PALLET COUNT]=GI[12]    ;
 121:  IF R[2:PALLET COUNT]=9,JMP LBL[1] ;
 122:  CALL DEP_LOOKTBL    ;
 123:  LBL[779] ;
 124:  IF DI[125]=ON,JMP LBL[1] ;
 125:  IF DI[122]=ON,JMP LBL[2] ;
 126:  JMP LBL[779] ;
 127:  ENDIF ;
 128:  ! ;
 129:  IF R[1:DEPALL COUNT]<>9,JMP LBL[2] ;
 130:  ! ;
 131:  !****************************** ;
 132:  !		INITIALIZATION ;
 133:  !****************************** ;
 134:  UFRAME_NUM=1 ;
 135:  UTOOL_NUM=5 ;
 136:  $WAITTMOUT=250 ;
 137:  LBL[1] ;
 138:  DO[121]=OFF ;
 139:  DO[122]=OFF ;
 140:  DO[123]=OFF ;
 141:  DO[124]=OFF ;
 142:  DO[125]=OFF ;
 143:  DO[126]=OFF ;
 144:  DO[127]=OFF ;
 145:  PL[1]=[1,1,9]    ;
 146:  PL[2]=[1,1,1]    ;
 147:  R[1:DEPALL COUNT]=9    ;
 148:  R[2:PALLET COUNT]=0    ;
 149:  R[3:Dropped parts]=0    ;
 150:  R[6:REJECT COUNTER]=0    ;
 151:  GO[11]=R[1:DEPALL COUNT] ;
 152:  GO[12]=R[2:PALLET COUNT] ;
 153:  GO[13]=R[3:Dropped parts] ;
 154:  !****************************** ;
 155:  ! MOVE TO READY, WAIT FOR DO[121] ;
 156:  !****************************** ;
 157:J P[1] 70% FINE    ;
 158:  R[3:Dropped parts]=0    ;
 159:  GO[13]=R[3:Dropped parts] ;
 160:  DO[121]=ON ;
 161:  ! ;
 162:  ! ;
 163:  ! BEGIN DEPALLETIZING CYCLE ;
 164:  ! ;
 165:  WAIT DI[121]=ON    ;
 166:  LBL[2] ;
 167:  DO[121]=OFF ;
 168:  DO[122]=ON ;
 169:  DO[123]=ON ;
 170:  GO[11]=R[1:DEPALL COUNT] ;
 171:  CALL DEP_DEPALLET    ;
 172:  R[1:DEPALL COUNT]=R[1:DEPALL COUNT]-1    ;
 173:  GO[11]=R[1:DEPALL COUNT] ;
 174:  DO[123]=OFF ;
 175:  DO[124]=ON ;
 176:  GO[12]=R[2:PALLET COUNT] ;
 177:  ! ;
 178:  ! ;
 179:  ! START CONDITION MONITORING PROG ;
 180:  ! ;
 181:  MONITOR DEP_CONDMON ;
 182:  ! ;
 183:  ! ;
 184:  ! START PALLET PROGRAM ;
 185:  ! ;
 186:  CALL DEP_PALLET    ;
 187:  R[2:PALLET COUNT]=R[2:PALLET COUNT]+1    ;
 188:  LBL[4] ;
 189:  GO[12]=R[2:PALLET COUNT] ;
 190:  DO[124]=OFF ;
 191:  ! ;
 192:  ! ;
 193:  ! ;
 194:  LBL[999] ;
 195:  DO[122]=OFF ;
 196:  IF R[1:DEPALL COUNT]=0,JMP LBL[1] ;
 197:  JMP LBL[2] ;
/POS
P[1]{
   GP1:
	UF : 1, UT : 5,		CONFIG : 'N U T, 0, 0, 0',
	X =  -129.508  mm,	Y =   108.794  mm,	Z =   449.995  mm,
	W =  -179.665 deg,	P =     -.366 deg,	R =   -25.099 deg
};
/END
